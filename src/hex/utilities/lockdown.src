lockdownHelp = function(self)
    text = g.const.ni + g.l3("Usage:","u") + g.const.ni
    text = text + g.g2(self.name) + g.l2(" Secures the system by changing permissions on all files and folders to u-rwx, g-rwx, o-rwx. If this is your home system, it leaves the ability to run terminal, sudo, mail, map, chat, settings, and manual. Also deletes all files named Mail.txt, Bank.txt, and passwd.") + g.const.ni
    text = text + g.g2(self.name) + g.b3(" [-h/help]") + g.l2(" Displays this help text") + g.const.nl

    print(text)
end function

lockdown = function(args = null)
    setHomePerms = function(obj)
        result = obj.chmod("g+rx")
        if result != "" then print(g.error(result))
    end function

    walk = function(file)
        excludes = ["sudo","Terminal","Terminal.exe","Mail","Mail.exe","Map","Map.exe","Chat","Chat.exe","Settings.exe","Manual","Manual.exe","autoexec"]

        for item in file.get_files
            if excludes.indexOf(item.name) != null then setHomePerms(item)
        end for

        for folder in file.get_folders
            walk(folder)
        end for
    end function

    del = function(files)
        for file in files
            print g.const.indent + g.o1("Deleting file: " + file.path)
            result = file.delete
            if result != "" then print g.error(result)
        end for
    end function

    if args != null and args.len > 0 then
        if args[0] == "help" or args[0] == "-h" then
            self.help
            return
        end if
    end if

    idx = g.context.index

    file = g.stack[idx].file
    comp = g.stack[idx].computer

    home = 0

    if comp != null then
        procs = comp.show_procs.split(char(10))[1:]

        for proc in procs
            name = proc.split(" ")[4]
            if name == "Xorg" then
                home = 1
                break
            end if
        end for
    end if

    if home then print(g.const.indent + g.b3("Home computer detected...") + g.const.nl)

    root = file.getRootFolder

    del(g.func.find(root, "guest", "fw"))

    del(g.func.find(root, "passwd", "t"))

    del(g.func.find(root, "Bank.txt", "tw"))

    del(g.func.find(root, "Mail.txt", "tw"))

    del(g.func.find(root, ".Trash", "fw"))

    root.chmod("u-rwx", 1)
    root.chmod("g-rwx", 1)
    root.chmod("o-rwx", 1)
    root.set_owner("root", 1)
    root.set_group("root", 1)

    if home then walk(root)

    print(g.const.ni + g.g2("System locked down."))
    return
end function

g.util.lockdown = {
    "name": "lockdown",
    "shortDescription": "Secures the filesystem on the current context's device.",
    "exec": @lockdown,
    "help": @lockdownHelp,
    "data": {}
}