scanHelp = function()
    text = g.const.ni + g.l3("Usage:","u") + g.const.ni
    text = text + g.g2(self.name) + g.b3(" [ip] {port}") + g.l2(" Scans for vulnerabilities at the specified ip address and port. If the port is omitted it will scan all open ports instead.") + g.const.ni
    text = text + g.g2(self.name) + g.b3(" [library.so]") + g.l2(" Scans for vulnerabilities in the specified local library.  Use the full file name for the library, including the '.so' extension.") + g.const.ni
    text = text + g.g2(self.name) + g.b3(" [-h/help]") + g.l2(" Display this help text") + g.const.nl

    print(text)
end function

doScan = function(args)
    idx = g.context.index
    context = g.stack[idx]
    if not g.tools.scan.data.hasIndex("silent") then g.tools.scan.data.silent = 0

    doOutput = function(metaLibs)
        results = {}

        for metaLib in metaLibs
            if not results.hasIndex(metaLib.lib_name) then
                results[metaLib.lib_name] = {}
            end if

            if not results[metaLib.lib_name].hasIndex(metaLib.version) then
                results[metaLib.lib_name][metaLib.version] = {}
            end if
        end for

        libs = results.indexes
        libs.sort

        output = g.const.nl + g.func.heading("Scan Results") + g.const.nl
        for lib in libs
            output = output + g.const.indent + g.s2("Library: " + lib) + g.const.nl

            versions = results[lib].indexes
            versions.sort

            for version in versions
                output = output + g.const.indent + "  " + g.s3("Version: " + version) + g.const.nl

                mems = g.database[lib][version].indexes
                mems.sort

                for mem in mems
                    output = output + g.const.indent + "    " + g.s4("Address: " + mem) + g.const.nl

                    values = g.database[lib][version][mem].indexes
                    values.sort

                    for value in values
                        output = output + g.const.indent + "      <size=80%>" + g.o1("Value: " + value + "<pos=550>Result: " + g.database[lib][version][mem][value]) + g.const.nl
                    end for
                end for
            end for
        end for
        clear_screen
        print output
        return 1
    end function

    scanMetaLibs = function(metaLibs)
        for metaLib in metaLibs
            if not g.database.hasIndex(metaLib.lib_name) then
                g.database[metaLib.lib_name] = {}
            end if

            if not g.database[metaLib.lib_name].hasIndex(metaLib.version) then
                g.database[metaLib.lib_name][metaLib.version] = {}
            end if

            rescan = 0

            // Check to see if we need to rescan this lib/version.
            mems = g.database[metaLib.lib_name][metaLib.version].indexes

            if not mems or mems.len == 0 then rescan = 1

            for mem in mems
                if rescan == 1 then break
                values = g.database[metaLib.lib_name][metaLib.version][mem].indexes

                if not values or values.len == 0 then rescan = 1

                for value in values
                    if rescan == 1 then break
                    if g.database[metaLib.lib_name][metaLib.version][mem][value] == "" then
                        rescan = 1
                        break
                    end if
                end for
            end for

            if rescan then
                mems = g.stack["0"].metax.scan(metaLib)

                for mem in mems
                    g.database[metaLib.lib_name][metaLib.version][mem] = {}

                    vulns = g.stack["0"].metax.scan_address(metaLib, mem).split("Unsafe check: ")[1:]
                    for vuln in vulns
                        value = vuln[vuln.indexOf("<b>")+3:vuln.indexOf("</b>")]

                        g.database[metaLib.lib_name][metaLib.version][mem][value] = ""
                    end for
                end for
            end if
        end for

        return 1
    end function

    scanLocal = function(libName)
        idx = g.context.index

        if not g.func.checkShell(idx) then return 0
        if not g.func.checkMetax(idx) then return 0

        metaLib = context.metax.load(libName)

        if not metaLib then
            print(g.error("Could not load library: " + libName))
            return 0
        end if

        metaLibs = [metaLib]
        scanMetaLibs(metaLibs)

        if not g.tools.scan.data.silent then
            return doOutput(metaLibs)
        end if

        return 1
    end function

    scanRemote = function(ip, port)
        idx = g.context.index

        if g.hasIndex("proxy") and g.proxy != "" and typeof(g.proxy.to_int) == "number" and not is_lan_ip(ip) then
            idx = g.proxy
            print g.g1(g.const.indent + "Using proxy: " + g.stack[idx].public)
        end if
        
        if not g.func.checkShell(idx) then return 0
        if not g.func.checkMetax(idx) then return 0

        netSession = g.stack[idx].metax.net_use(ip, port)

        if typeof(netSession) == "string" then
            print g.error(netSession)
            return 0
        else if netSession == null then
            print g.error("Could not connect to " + ip + ":" + port)
            return 0
        end if

        metaLib = netSession.dump_lib

        metaLibs = [metaLib]

        scanMetaLibs(metaLibs)
        if not g.tools.scan.data.silent then
            return doOutput(metaLibs)
        end if

        return 1
    end function

    scanAllPorts = function(ip)
        idx = g.context.index

        if g.hasIndex("proxy") and g.proxy != "" and typeof(g.proxy.to_int) == "number" and not is_lan_ip(ip) then
            idx = g.proxy
            print g.g1(g.const.indent + "Using proxy: " + g.stack[idx].public)
        end if

        if not g.func.checkShell(idx) then return 0
        if not g.func.checkMetax(idx) then return 0

        router = g.func.getRouter(ip)

        if not is_lan_ip(ip) then
            ports = router.used_ports
            isRouter = 1
        else
            ports = router.device_ports(ip)
            isRouter = 0
        end if

        metaLibs = []

        for port in ports
            if port.port_number == 8080 then isRouter = 1
            if port.is_closed then continue

            netSession = g.stack[idx].metax.net_use(ip, port.port_number)

            if not netSession then
                print(g.error("Could not connect to " + ip + ":" + port.port_number))
            else if typeof(netSession) == "string" then
                print(g.error(netSession))
            else
                metaLib = netSession.dump_lib

                metaLibs.push(metaLib)
            end if
        end for

        if isRouter then
            netSession = g.stack[idx].metax.net_use(ip, 0)

            metaLib = netSession.dump_lib

            metaLibs.push(metaLib)
        end if

        scanMetaLibs(metaLibs)
        
        if not g.tools.scan.data.silent then
            return doOutput(metaLibs)
        end if

        return 1
    end function

    if not args or args == [] then
        self.help

        return 0
    else if args.len == 1 then
        arg = args[0]

        if arg == "-h" or arg == "help" then
            self.help

            return 1
        else if not is_valid_ip(arg) then
            if not arg.is_match(".+\.so") then
                print g.error("Invalid library name. (Usage: scan [library.so])")
                return 0
            end if

            return scanLocal("/lib/" + arg)
        else if is_valid_ip(arg) then
            return scanAllPorts(arg)
        else
            print(g.error("Invalid argument: " + arg))
            
            return 0
        end if
    else if args.len == 2 then
        address = args[0]
        port = args[1]

        if not is_valid_ip(address) then
            print(g.const.indent + g.error("Invalid IP address: " + address))
            return 0
        end if

        if port.val == null or port.to_int < 0 or port.to_int > 65535 then
            print(g.const.indent + g.error("Invalid port number: " + port))
            return 0
        end if

        return scanRemote(address, port.to_int)
    end if
end function

g.tools.scan = {
    "name": "scan",
    "shortDescription": "Scans for vulnerabilities.",
    "exec": @doScan,
    "help": @scanHelp,
    "data": {}
}