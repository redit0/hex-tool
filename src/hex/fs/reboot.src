// Prints usage and flag descriptions for the reboot fs command.
// @return {none}
rebootHelp = function
    text = g.const.ni + g.l3("Usage:","u") + g.const.ni
    text = text + g.g2(self.name) + g.l2(" Reboots the target device if the context is a shell or computer object.") + g.const.ni
    text = text + g.g2(self.name) + g.b3(" [-h/help]") + g.l2(" Displays this help text") + g.const.nl

    print(text)
end function



// Reboots the computer of the current context and removes it from the context stack.
// Resets the active context index to "0" (home) before rebooting.
// @param {list<string>|null} args - Pass ["-h"] or ["help"] to show usage, or null/[] to reboot.
// @return {number} - 1 on success, 0 on failure.
doReboot = function(args = null)
    if args != null and args isa list and args.len == 1 and (args[0] == "-h" or args[0] == "help") then
        self.help
        return 1
    end if

    if args == null or args == [] then
        idx = g.context.index
        
        if g.stack[idx].computer == null then
            print g.error("Context must be a shell or computer")
            return 0
        end if

        g.context.index = "0"
        result = g.stack[idx].computer.reboot
        if result isa string then
            print g.error(result)
            return 0
        end if
        g.stack.remove(idx)

        return 1
    end if

    return 0
end function

g.fs.reboot = {
    "name": "reboot",
    "shortDescription": "Reboots the target",
    "exec": @doReboot,
    "help": @rebootHelp,
}