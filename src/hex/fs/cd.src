// Prints usage and flag descriptions for the cd fs command.
// @return {none}
changeDirHelp = function
    text = g.const.ni + g.l3("Usage:","u") + g.const.ni
    text = text + g.g2(self.name) + g.b3(" [folder name or path]") + g.l2(" Switch to the specified directory.") + g.const.ni
    text = text + g.g2(self.name) + g.b3(" [-h/help]") + g.l2(" Displays this help text") + g.const.nl

    print(text)
end function



// Changes the current working directory of the active context to the specified folder.
// Supports absolute paths, relative names, ".." to go up one level, "." to stay, and "/" for root.
// @param {list<string>|null} args - One element: a folder name, path, "..", ".", or "/".
// @return {number} - 1 on success, 0 on failure.
changeDir = function(args = null)
    // Recursively searches the directory tree for a folder matching the given name or path.
    // @param {file} file - The directory to search within.
    // @return {file|null} - The matching folder, or null if not found.
    checkFile = function(file)
        files = file.get_folders

        for f in files
            if f.name == args[0] or f.path == args[0] then return f

            retVal = checkFile(f)

            if retVal then return retVal
        end for

        return null
    end function

    if args == null or args == [] or args.len > 1 or args[0] == "-h" or args[0] == "help" then
        changeDirHelp
        return 1
    end if

    if typeof(args[0] == "string") then
        context = g.stack[g.context.index]
        root = context.file.getRootFolder

        if args[0] == "/" then
            context.currentPath = "/"
            return 1
        else if args[0] == ".." then
            if context.currentPath == "/" then return

            path = context.currentPath.split("/")
            path.pop

            context.currentPath = path.join("/")
            if context.currentPath == "" then context.currentPath = "/"
            return 1
        else if args[0] == "." then
            return 1
        else
            file = checkFile(root)

            if file then
                context.currentPath = file.path
                return 1
            end if
        end if

        print(g.error("No such directory."))
        return 0
    else
        print(g.error("Invalid argument type."))
        return 0
    end if
end function

g.fs.cd = {
    "name": "cd",
    "shortDescription": "Switch to the specified directory.",
    "exec": @changeDir,
    "help": @changeDirHelp,
}